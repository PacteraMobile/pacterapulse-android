/*
 * Copyright (c) 2015 Pactera. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * THIS CODE IS PROVIDED AS IS BASIS, WITHOUT WARRANTIES OR CONDITIONS
 * OF ANY KIND, EITHER EXPRESS OR IMPLIED, INCLUDING WITHOUT LIMITATION
 * ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE, FITNESS FOR A
 * PARTICULAR PURPOSE, MERCHANTABILITY OR NON-INFRINGEMENT.
 *
 * See the Apache License, Version 2.0 for the specific language
 * governing permissions and limitations under the License.
 */



buildscript {
	repositories {
		jcenter()
		maven { url 'https://maven.fabric.io/public' }
	}

	dependencies {
		classpath 'io.fabric.tools:gradle:1.19.0'
	}
}


repositories {
	jcenter()
	maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'


// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 4

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
def time = new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
	signingConfigs {
		Pactera {
			keyPassword '1a2b3c4d5e'
			storeFile file('../keys/Pactera.jks')
			storePassword '1a2b3c4d5e'
			keyAlias 'PacteraRelease'
		}
	}
	compileSdkVersion 22
	buildToolsVersion "22.0.1"

	dexOptions {
		// Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
		preDexLibraries = preDexEnabled && !isTravis
	}

	packagingOptions {
		exclude 'META-INF/DEPENDENCIES'
		exclude 'META-INF/NOTICE'
		exclude 'META-INF/LICENSE'
		exclude 'LICENSE.txt'
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/NOTICE.txt'
		exclude 'META-INF/services/javax.annotation.processing.Processor'
	}


	defaultConfig {
		applicationId "au.com.pactera.pacterapulse"
		minSdkVersion 14
		targetSdkVersion 22

		versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch * 1
		versionName "${versionMajor}.${versionMinor}.${versionPatch}"
		buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
		buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

		testInstrumentationRunner "au.com.pactera.pacterapulse.JUnitJacocoTestRunner";
	}

	buildTypes {
		debug {
			applicationIdSuffix '.dev'
			versionNameSuffix '-dev'
			//minifyEnabled false
			testCoverageEnabled true
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.Pactera
		}
	}

	jacoco {
		version "0.7.5.201505241946"
	}
}

dependencies {
	compile fileTree(include: ['*.jar'], dir: 'libs')
	compile 'com.android.support:support-v4:22.2.0'
	compile 'com.jakewharton:butterknife:6.1.0'
	compile 'com.github.kevinsawicki:http-request:5.6'
	compile('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') {
		transitive = true;
	}
	compile('de.keyboardsurfer.android.widget:crouton:1.8.5@aar') {
		exclude group: 'com.google.android', module: 'support-v4'
	}
	compile('com.microsoft.aad:adal:1.1.5') {
		exclude group: 'com.android.support'
	}

	// Espresso Instrumentation Testing
	androidTestCompile 'com.android.support:support-annotations:22.2.0'
	androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
	androidTestCompile 'com.android.support.test:runner:0.3'

	// Fixes for Linux and Mac
	testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'

	// Junit
	testCompile 'junit:junit:4.12'

	// Hamcrest Matchers for Junit
//	testCompile 'org.hamcrest:hamcrest-integration:1.3'

	// Mockito
	testCompile 'org.mockito:mockito-core:1.10.19'

	// EasyMock
	testCompile 'org.easymock:easymock:3.3.1'

	// PowerMock
	testCompile 'org.powermock:powermock-core:1.6.2'
	testCompile 'org.powermock:powermock-module-junit4:1.6.2'

	// PowerMock + EasyMock extension
	testCompile 'org.powermock:powermock-api-easymock:1.6.2'
}

jacoco {
	toolVersion = "0.7.5.201505241946"
}

coveralls {
	jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}
